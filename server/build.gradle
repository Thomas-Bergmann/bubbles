plugins {
    id 'maven-publish'
    id "io.spring.dependency-management" version "1.1.6"
}

allprojects {
    group = 'de.hatoka.bubbles'
    version = '1.0.0-LOCAL'

    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    repositories {
        // mavenLocal()
        mavenCentral()
        // maven { url "https://s01.oss.sonatype.org/content/repositories/releases" }
	}

    jacoco { toolVersion = '0.8.12' }
}

def versionMicrosoftSqlserverMssqlJdbc="12.4.2.jre11"
def versionGlassfishJersey="3.1.7"
def versionSpringDoc="2.6.0"
def versionJJWT="0.12.6"

subprojects {
    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    // need to exclude to avoid Class path containing multiple SLF4J bindings
    configurations {
        all.collect { configuration ->
            configuration.exclude group: 'org.slf4j', module: 'slf4j-simple'
        }
    }

    apply plugin: 'io.spring.dependency-management'
    dependencies {
        dependencyManagement {
            imports { mavenBom("org.springframework.boot:spring-boot-dependencies:3.3.2") }
            importedProperties['javax-jaxb.version']

            dependencies {
                dependency "org.springdoc:springdoc-openapi-starter-webmvc-api:${versionSpringDoc}"
                dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:${versionSpringDoc}"

                dependency 'com.nimbusds:nimbus-jose-jwt:9.40'

                dependency "net.logstash.logback:logstash-logback-encoder:8.0"

                dependency 'de.hatoka.neuralnetwork:neuralnetwork:2.0.0'

                dependency "org.glassfish.jersey.core:jersey-client:${versionGlassfishJersey}"
                dependency "org.glassfish.jersey.core:jersey-common:${versionGlassfishJersey}"
                dependency "org.glassfish.jersey.inject:jersey-hk2:${versionGlassfishJersey}"
                dependency "org.glassfish.jersey.media:jersey-media-json-jackson:${versionGlassfishJersey}"
                dependency 'org.json:json:20240205'

				dependency 'jakarta.validation:jakarta.validation-api:3.0.2'
                dependency 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'

                dependency "io.jsonwebtoken:jjwt-api:${versionJJWT}"
                dependency "io.jsonwebtoken:jjwt-impl:${versionJJWT}"
                dependency "io.jsonwebtoken:jjwt-jackson:${versionJJWT}"

                dependency "org.liquibase:liquibase-core:4.28.0"
                dependency 'info.picocli:picocli:4.7.6'
                dependency "com.microsoft.sqlserver:mssql-jdbc:${versionMicrosoftSqlserverMssqlJdbc}"
            }
        }
    }

    test {
        useJUnitPlatform {
            excludeEngines 'junit-vintage'
        }
    }

    jacocoTestReport {
        reports {
            csv.required = false
            xml.required = true
            html.required = true
            html.outputLocation = project.layout.buildDirectory.dir("reports/jacoco/html")
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Title': name,
                   'Implementation-Version': project.version
        }
    }
}

repositories {
    mavenCentral()
}

// based on http://www.jroller.com/aalmiray/entry/gradle_glam_jacoco_coveralls
task jacocoReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.jacocoTestReport
    reports {
        csv.required = false
        xml.required = false
        html.required = true
        html.outputLocation = project.layout.buildDirectory.dir("reports/jacoco/html")
    }
}
